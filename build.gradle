buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.netflix.nebula:nebula-release-plugin:6.1.0'
        classpath 'com.netflix.nebula:nebula-bintray-plugin:3.5.2'
        classpath('org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4') {
            force = true
        }
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}


allprojects {
    group = 'actor.proto'

    apply plugin: 'idea'
    apply plugin: 'nebula.release'
}

subprojects {
    apply plugin: 'kotlin'

    repositories {
        jcenter()
    }

    dependencies {
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
        compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.19.3'

        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
        testCompile group: 'junit', name: 'junit', version:'4.12'
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            allWarningsAsErrors = true
        }
    }
}

configure(subprojects - project(':examples')) {
    apply from: "$rootDir/gradle/publishing.gradle"
    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport
}

gradle.taskGraph.whenReady { graph ->
    tasks.artifactoryPublish.onlyIf {
        graph.hasTask(':snapshot')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}
